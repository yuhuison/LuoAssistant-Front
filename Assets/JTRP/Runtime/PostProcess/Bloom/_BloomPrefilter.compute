#include "Packages/com.unity.render-pipelines.core/ShaderLibrary/Common.hlsl"
#include "Packages/com.unity.render-pipelines.core/ShaderLibrary/Color.hlsl"
#include "Packages/com.unity.render-pipelines.high-definition/Runtime/ShaderLibrary/ShaderVariables.hlsl"
#include "Packages/com.unity.render-pipelines.high-definition/Runtime/PostProcessing/Shaders/BloomCommon.hlsl"

#pragma only_renderers d3d11 ps4 xboxone vulkan metal switch

#pragma kernel KMain

TEXTURE2D_X(_InputTexture);

RW_TEXTURE2D_X(float3, _OutputTexture);

SAMPLER(sampler_LinearClamp);

CBUFFER_START(cb0)
    float4 _TexelSize;
    float4 _BloomThreshold;
CBUFFER_END

#define GROUP_SIZE 8

[numthreads(GROUP_SIZE, GROUP_SIZE, 1)]
void KMain(uint3 dispatchThreadId : SV_DispatchThreadID)
{
    UNITY_XR_ASSIGN_VIEW_INDEX(dispatchThreadId.z);
    PositionInputs posInputs = GetPositionInput(float2(dispatchThreadId.xy), _TexelSize.zw, uint2(GROUP_SIZE, GROUP_SIZE));
    float2 uv = posInputs.positionNDC;

    // Use a rotated grid to minimize artifacts coming from horizontal and vertical boundaries
    // "High Quality Antialiasing" [Lorach07]
    const float2 kTaps[] = {
        float2( 0.0,  0.0),
        float2( 0.9, -0.4),
        float2(-0.9,  0.4),
        float2( 0.4,  0.9),
        float2(-0.4, -0.9)
    };

//===========================JTRP===========================
    float4 color0 = SAMPLE_TEXTURE2D_X_LOD(_InputTexture, sampler_LinearClamp, ClampAndScaleUVForBilinear(_TexelSize.zw * kTaps[0] + uv, _TexelSize.zw), 0.0);
    float4 color1 = SAMPLE_TEXTURE2D_X_LOD(_InputTexture, sampler_LinearClamp, ClampAndScaleUVForBilinear(_TexelSize.zw * kTaps[1] + uv, _TexelSize.zw), 0.0);
    float4 color2 = SAMPLE_TEXTURE2D_X_LOD(_InputTexture, sampler_LinearClamp, ClampAndScaleUVForBilinear(_TexelSize.zw * kTaps[2] + uv, _TexelSize.zw), 0.0);
    float4 color3 = SAMPLE_TEXTURE2D_X_LOD(_InputTexture, sampler_LinearClamp, ClampAndScaleUVForBilinear(_TexelSize.zw * kTaps[3] + uv, _TexelSize.zw), 0.0);
    float4 color4 = SAMPLE_TEXTURE2D_X_LOD(_InputTexture, sampler_LinearClamp, ClampAndScaleUVForBilinear(_TexelSize.zw * kTaps[4] + uv, _TexelSize.zw), 0.0);

    color0.rgb = QuadraticThreshold(color0.rgb, _BloomThreshold.x, _BloomThreshold.yzw);
    color1.rgb = QuadraticThreshold(color1.rgb, _BloomThreshold.x, _BloomThreshold.yzw);
    color2.rgb = QuadraticThreshold(color2.rgb, _BloomThreshold.x, _BloomThreshold.yzw);
    color3.rgb = QuadraticThreshold(color3.rgb, _BloomThreshold.x, _BloomThreshold.yzw);
    color4.rgb = QuadraticThreshold(color4.rgb, _BloomThreshold.x, _BloomThreshold.yzw);

    // Luma weighted average [Karis13]
    float w0 = 1.0 / (Luminance(color0.rgb) + 1.0);
    float w1 = 1.0 / (Luminance(color1.rgb) + 1.0);
    float w2 = 1.0 / (Luminance(color2.rgb) + 1.0);
    float w3 = 1.0 / (Luminance(color3.rgb) + 1.0);
    float w4 = 1.0 / (Luminance(color4.rgb) + 1.0);
    float wSum = w0 + w1 + w2 + w3 + w4;

    color0.rgb *= w0 * color0.a;
    color1.rgb *= w1 * color1.a;
    color2.rgb *= w2 * color2.a;
    color3.rgb *= w3 * color3.a;
    color4.rgb *= w4 * color4.a;

    float3 acc = color0.rgb + color1.rgb + color2.rgb + color3.rgb + color4.rgb;
//===========================JTRP===========================
    float3 output = acc / wSum;

    // Guard bands
    output *= all(dispatchThreadId.xy < uint2(_TexelSize.xy));

    _OutputTexture[COORD_TEXTURE2D_X(posInputs.positionSS)] = output;
}
